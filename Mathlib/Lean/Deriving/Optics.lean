/-
Copyright (c) 2022 E.W.Ayers. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: E.W.Ayers
-/
import Lean
import Lean.Parser
import Mathlib.Data.String.Defs
open Lean Elab Command Term Tactic
open Lean.Parser.Term
open Lean.Parser.Command
open Lean.Elab.Deriving

/-!

# Deriving optics from inductive datatypes.

This file defines the `derive_optics T` command where `T` is an inductive datatype.
For each constructor `ùëê` of `T` and each field `ùëé : Œ±` of `ùëê`, this will create the following definitions:

1. `T.ùëê.ùëé? : T ‚Üí Option Œ±`
2. `T.ùëê.ùëé! : T ‚Üí Œ±`
3. `T.ùëê.withùëé : Œ± ‚Üí T ‚Üí T`
4. `T.ùëê.modifyùëé : (Œ± ‚Üí Œ±) ‚Üí T ‚Üí T`
5. `T.ùëê.modifyMùëé : (Œ± ‚Üí M Œ±) ‚Üí T ‚Üí M T`

## Future work

[todo] Extending to many other patterns:

- `T.children : T ‚Üí List T`
- `T.traverseChildren [Applicative M]: (T ‚Üí M T) ‚Üí (T ‚Üí M T)`
- `T.Base : Type ‚Üí Type` is the base functor type such that `T = Fix T.Base`
- `T.Free : Type ‚Üí Type`
- `T.Zipper`
- `T.Pos` -- analogous to `Expr.SubExpr.Pos`.
- Build an optics library and have full-fledged optics.

-/

namespace Lean.Elab.Deriving.Optics

initialize registerTraceClass `derive_optics

-- [todo] this must already exist?
def Name.mapHead (f : String ‚ÜíString) : Name ‚ÜíName
  | Name.str p s _ => Name.mkStr p (f s)
  | n => n

def mkDocComment (s : String) : Syntax :=
  mkNode ``Lean.Parser.Command.docComment #[mkAtom "/--", mkAtom (s ++ "-/")]

variable {M} [MonadControlT MetaM M] [MonadLiftT MetaM M] [Monad M] [MonadEnv M] [MonadError M]

structure IndField :=
  (ctor : Name)
  (name : Name)
  (index : Nat)
  /-- Abstracted on params. Use `type.instantiateRev params` to reinstantiate. -/
  (type : Expr)

/-- Maps a field name to the constructors which include that field name and the type.
It's none if the field exists on constructors but the types are incompatible.-/
abbrev FieldCollections := NameMap (Option (NameMap Nat √ó Expr))

def getAllFields (decl : Name) : TermElabM (Array IndField) := do
  let indVal ‚Üê getConstInfoInduct decl
  indVal.ctors.foldlM (fun acc ctor => do
    let ctorInfo ‚Üê Lean.getConstInfoCtor ctor
    Lean.Meta.forallTelescopeReducing ctorInfo.type fun xs type => do
      let xsdecls ‚Üê liftM $ xs.mapM Lean.Meta.getFVarLocalDecl
      let params := xs[:ctorInfo.numParams].toArray
      let fields := xsdecls[ctorInfo.numParams:].toArray
      let field_idxs : Array (Nat √ó _) := fields.mapIdx fun i x => (i,x)
      field_idxs.foldlM (fun acc (fieldIdx, field) => do
        let fieldName := field.userName
        if fieldName.isNum then
          return acc
        let type := Expr.abstract field.type params
        return acc.push ‚ü®ctor, fieldName, fieldIdx, type‚ü©
      ) acc
  ) #[]

/-- Given inductive datatype `decl`, makes a map from field names to a
map from constructor names to field index and type. -/
def getFieldCollections
  (decl : Name) : TermElabM FieldCollections := do
  let fields ‚Üê getAllFields decl
  return fields.foldl add ‚àÖ
  where
    add (n : FieldCollections) (f : IndField) : FieldCollections :=
      match n.find? f.name with
      | some x => x.bind (fun (ctors, t) => if t == f.type && not (ctors.contains f.ctor) then some (ctors.insert f.ctor f.index, t) else none) |> n.insert f.name
      | none => n.insert f.name (some (NameMap.insert ‚àÖ f.ctor f.index, f.type))

private def mkAlt (mkRhs : (fieldVars: Array Syntax) ‚Üí TermElabM Syntax) (ctor : Name) : TermElabM (Syntax √ó Syntax) := do
  let ctorInfo ‚Üê Lean.getConstInfoCtor ctor
  let fieldVars ‚Üê
    List.range ctorInfo.numFields
    |>.mapM (fun _ => mkIdent <$> mkFreshUserName `a)
  let fieldVars := fieldVars.toArray
  let lhs ‚Üê `($(mkIdent ctorInfo.name):ident $fieldVars:term*)
  let rhs ‚Üê mkRhs fieldVars
  return (lhs, rhs)

private def mkAlts (ctors : NameMap Nat) (mkRhs : (ctorName : Name) ‚Üí (fieldIdx : Nat) ‚Üí (fieldVars : Array Syntax) ‚Üí TermElabM Syntax) : TermElabM ((Array Syntax) √ó (Array Syntax)) := do
  let cs ‚Üê ctors.toList.toArray.mapM (fun (n,i) => mkAlt (mkRhs n i) n)
  return Array.unzip cs

private def ctorNameOrList (ctors : NameMap Œ±) : String :=
  ctors.toList |>.map Prod.fst |>.map (fun | Name.str _ x _ => s!"`{x}`" | _ => "????") |> String.andList "or"

private def isExhaustive (ctors : NameMap Œ±) (indName : Name) : M Bool := do
  let indVal ‚Üê getConstInfoInduct indName
  return indVal.ctors.all (fun a => ctors.contains a)

def mkGetOptional (baseName indName fieldName : Name) (indType : Syntax) (implicitBinders : Array Syntax) (fieldType : Syntax) (ctors : NameMap Nat) : TermElabM Syntax := do
  if (‚Üê isExhaustive ctors indName) then
    throwError "expected non-exhautive ctor list"
  let defname := mkIdent <| baseName ++ Name.mapHead (¬∑ ++ "?") fieldName
  let (lhs, rhs) ‚Üê mkAlts ctors (fun _ i fvs => `(some $(fvs[i])))
  let docstring := mkDocComment <| s!"If the given `{indName}` is a {ctorNameOrList ctors}; returns the value of the `{fieldName}` field, otherwise returns `none`."
  `(
        $docstring:docComment
        def $defname:ident $implicitBinders:explicitBinder*
          : $indType ‚Üí Option $fieldType
          $[| $lhs => $rhs]*
          | _ => none
  )

def mkGetBang (baseName indName fieldName : Name) (indType : Syntax) (implicitBinders : Array Syntax) (fieldType : Syntax) (ctors : NameMap Nat) : TermElabM Syntax := do
  if (‚Üê isExhaustive ctors indName) then
    throwError "expected non-exhautive ctor list"
  let defname : Name := baseName ++ Name.mapHead (¬∑ ++ "!") fieldName
  let docstring := mkDocComment <| s!"If the given `{indName}` is a {ctorNameOrList ctors},
    returns the value of the `{fieldName}` field, otherwise panics."
  let (lhs, rhs) ‚Üê mkAlts ctors (fun _ i fvs => pure fvs[i])
  `(
    $docstring:docComment
    def $(mkIdent defname):ident $implicitBinders:explicitBinder* [Inhabited $fieldType]
      : $indType ‚Üí $fieldType
      $[| $lhs => $rhs]*
      | x =>
        let n := $(quote (ctorNameOrList ctors))
        panic! s!"expected constructor {n}"
  )

def mkGet (baseName indName fieldName : Name) (indType : Syntax) (implicitBinders : Array Syntax) (fieldType : Syntax) (ctors : NameMap Nat) : TermElabM Syntax := do
  if not (‚Üê isExhaustive ctors indName) then
    throwError "expected exhaustive ctor list"
  let defname : Name := baseName ++ fieldName
  let docstring := mkDocComment <| s!"Returns the value of the `{fieldName}` field."
  let (lhs, rhs) ‚Üê mkAlts ctors (fun _ i fvs => pure fvs[i])
  `(
    $docstring:docComment
    def $(mkIdent defname):ident $implicitBinders:explicitBinder* [Inhabited $fieldType]
      : $indType ‚Üí $fieldType
      $[| $lhs => $rhs]*
  )


def mkWith (baseName indName fieldName : Name) (indType : Syntax) (implicitBinders : Array Syntax) (fieldType : Syntax) (ctors : NameMap Nat) : TermElabM Syntax := do
  let defname : Name := baseName ++ Name.mapHead (fun n => s!"with{n.capitalize}") fieldName
  let x ‚Üê mkIdent <$> mkFreshUserName `x
  let (lhs, rhs) ‚Üê mkAlts ctors (fun ctorName i fvs => `($(mkIdent ctorName) $(fvs.modify i (fun _ => x)):term*))
  if ‚Üê isExhaustive ctors indName then
    `(
      $(mkDocComment <| s!"Replaces the value of the `{fieldName}` field with the given value."):docComment
      def $(mkIdent defname):ident $implicitBinders:explicitBinder*
        ($x : $fieldType)
        : $indType ‚Üí $indType
        $[| $lhs => $rhs]*
    )
  else
    `(
      $(mkDocComment <| s!"If the given `{indName}` is a {ctorNameOrList ctors},
          replaces the value of the `{fieldName}` field with the given value.
          Otherwise acts as the identity function."):docComment
      def $(mkIdent defname):ident $implicitBinders:explicitBinder*
        ($x : $fieldType)
        : $indType ‚Üí $indType
        $[| $lhs => $rhs]*
        | y => y
    )

def mkModify (baseName indName fieldName : Name) (indType : Syntax) (implicitBinders : Array Syntax) (fieldType : Syntax) (ctors : NameMap Nat) : TermElabM Syntax := do
  let defname : Name := baseName ++ Name.mapHead (fun n => s!"modify{n.capitalize}") fieldName
  let x ‚Üê mkIdent <$> mkFreshUserName `visit
  let (lhs, rhs) ‚Üê mkAlts ctors (fun ctorName i fvs => do
    let outFields ‚Üê fvs.modifyM i (fun q => `(($x <| $q)))
    `($(mkIdent ctorName) $outFields:term*))
  if ‚Üê isExhaustive ctors indName then
    `(
      $(mkDocComment <| s!"Modifies the value of the `{fieldName}` field with the given `visit` function."):docComment
      def $(mkIdent defname):ident $implicitBinders:explicitBinder*
        ($x :$fieldType ‚Üí $fieldType )
        : $indType ‚Üí $indType
        $[| $lhs => $rhs]*
    )
  else
    `(
      $(mkDocComment <| s!"If the given `{indName}` is a {ctorNameOrList ctors};
          modifies the value of the `{fieldName}` field with the given `visit` function."):docComment
      def $(mkIdent defname):ident $implicitBinders:explicitBinder*
        ($x :$fieldType ‚Üí $fieldType )
        : $indType ‚Üí $indType
        $[| $lhs => $rhs]*
        | y => y
    )


def mkModifyM (baseName indName fieldName : Name) (indType : Syntax) (implicitBinders : Array Syntax) (fieldType : Syntax) (ctors : NameMap Nat) : TermElabM Syntax := do
  let visit ‚Üê mkIdent <$> mkFreshUserName `visit
  let x ‚Üê mkIdent <$> mkFreshUserName `x
  let (lhs, rhs) ‚Üê mkAlts ctors (fun ctorName i fvs => do
    let outFields := fvs.modify i (fun q => x)
    `((fun $x => $(mkIdent ctorName) $outFields:term*) <$> $visit $(fvs[i])))
  let defname : Name := baseName ++ Name.mapHead (fun n => s!"modifyM{n.capitalize}") fieldName
  if ‚Üê (isExhaustive ctors indName) then
    let docstring := mkDocComment <|  s!"Runs the given `visit` function on the `{fieldName}` field."
    `(
      $docstring:docComment
      def $(mkIdent defname):ident
        {M} [Functor M]
        $implicitBinders:explicitBinder*
        ($visit : $fieldType ‚Üí M $fieldType)
        :  $indType ‚Üí M $indType
        $[| $lhs => $rhs]*
    )
  else
    let docstring := mkDocComment <|  s!"Runs the given `visit` function on the `{fieldName}` field if present.
            Performing the pure op if the given `{indName}` is not a {ctorNameOrList ctors}."
    `(
      $docstring:docComment
      def $(mkIdent defname):ident
        {M} [Pure M] [Functor M]
        $implicitBinders:explicitBinder*
        ($visit : $fieldType ‚Üí M $fieldType)
        :  $indType ‚Üí M $indType
        $[| $lhs => $rhs]*
        | y => pure y
    )

def opticMakers := [mkGet, mkGetOptional, mkGetBang, mkWith, mkModify, mkModifyM]

def mkOpticsCore (indVal : InductiveVal) : TermElabM (Array Syntax) :=
  Lean.Meta.forallTelescopeReducing indVal.type fun params indType => do
    let paramDecls ‚Üê liftM $ params.mapM Lean.Meta.getFVarLocalDecl
    let paramStx := paramDecls |>.map (fun x => mkIdent x.userName)
    let indType ‚Üê `($(mkIdent indVal.name):ident $paramStx:term*)
    let implicitBinders ‚Üê paramDecls |>.mapM (fun x => `(implicitBinderF| { $(mkIdent x.userName) }))
    let mut cmds := #[]
    let fcs ‚Üê getFieldCollections indVal.name
    have : ForIn TermElabM FieldCollections (_ √ó _) := Std.RBMap.instForInRBMapProd
    have : ForIn TermElabM (NameMap Nat) (_ √ó _) := Std.RBMap.instForInRBMapProd
    for (field, cne?) in fcs do
      if let some (ctors, fieldType) := cne? then
        let isEx := if ‚Üê isExhaustive ctors indVal.name then "exhaustive" else "non-exhaustive"
        trace[derive_optics] "Deriving optic functions for {isEx} field {field} with constructors {ctors.toList}. "
        let fieldType ‚Üê PrettyPrinter.delab <| fieldType.instantiateRev params
        for mk in opticMakers do
          try
            let cmd ‚Üê mk indVal.name indVal.name field indType implicitBinders fieldType ctors
            cmds := cmds.push cmd
          catch
            | x => continue
    let fields ‚Üê getAllFields indVal.name
    for field in fields do
      let fieldType ‚Üê PrettyPrinter.delab <| field.type.instantiateRev params
      let ctors := mkNameMap Nat |>.insert field.ctor field.index
      for mk in opticMakers do
        try
          let cmd ‚Üê mk field.ctor indVal.name field.name indType implicitBinders fieldType ctors
          cmds := cmds.push cmd
        catch
          | e => continue
    return cmds

def mkOptics (decl : Name) : CommandElabM Unit := do
  if not (‚Üê isInductive decl) then
    throwError "{decl} must be an inductive datatype."
  let indVal ‚Üê getConstInfoInduct decl
  if isStructure (‚Üê getEnv) indVal.name then
    throwError "{decl} structures have projectors already"
  if indVal.numIndices > 0 then
    -- It should be possible to auto-derive some optics using the method as below
    -- But the result is usually confusing so it's better to not support it and
    -- get the users to make bespoke optics.
    throwError "getters and setters derivation not supported for indexed inductive datatype {decl}."
  if indVal.ctors.length <= 1 then
    -- [todo] add lens def here.
    throwError "single constructor inductive types should be structures."

  let cmds : Array Syntax ‚Üê liftTermElabM none <| mkOpticsCore indVal
  trace[derive_optics] "Created {cmds.size} definitions."
  for cmd in cmds do
    let pp ‚Üê liftCoreM $ PrettyPrinter.ppCommand cmd
    trace[derive_optics] "Creating definition:\n{pp}"
    elabCommand cmd

elab "derive_optics" decl:ident : command =>
  mkOptics decl.getId

end Lean.Elab.Deriving.Optics
